apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral();
}

dependencies {
    compile(
            'junit:junit:4.11',
    )
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.groupId = "com.thoughtworks"
            pom.version = '0.1-snapshot'
            pom.artifactId = 'toggle'

            pom.project {
                name project.name
                packaging 'jar'
                description 'Toggle is a little java tool to make your life easier when you try to use Feature Toggle in Java.'
                url 'https://github.com/xianlinbox/Toggle'

                scm {
                    url 'scm:git@github.com:xianlinbox/Toggle.git'
                    connection 'scm:git@github.com:xianlinbox/Toggle.git'
                    developerConnection 'git@github.com:xianlinbox/Toggle.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'xianlinbox'
                        name 'Liu Xiannings'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}